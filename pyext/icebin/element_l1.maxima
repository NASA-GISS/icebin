/* This is a maxima file.  To run:
      maxima <element3
*/

/*

Formula of a plane: z = Ax + By + C
   (xy means a vector with components x, and y)

Coefficients A,B an C can e obtained by solving:
	Ap0x + Bp0y + C = p0z
	Ap1x + Bp1y + C = 0
	AP2x + Bp2y + C = 0

Now... Suppose that f(x,y) = f(xy) = Ax + By + C inside one element.
Express integral inside a closed polygon as a line integral around the
perimeter of that polygon.  FF_xy() is what we will line-integrate.

 https://en.wikipedia.org/wiki/Green's_theorem
*/
FF_xy(x,y) := [
	-(C/2 + A*x)*y,
	(C/2 + B*y)*x];

/* Vector form of FF_xy */
FF(xy) := FF_xy(xy[1], xy[2]);

/* To meet the conditions of Green's Theorem:
Verify that dF2/dx - dF1/dy = A + Bx + Cy = A + B xx[1] + C xx[2] */
print("Should be zero: ",
	diff(FF([x,y])[2], x) - diff(FF([x,y])[1], y)
	- (A*x + B*y + C))$


/* In this symbolic code, we will integrate just one line segment of
the polygon.  For that, we need to introduce a parameterization, which
we use to line-integrate FF([x,y]).

q1 and q2 are beginning and end points of our line segment.
rr_{q1,q2}(t) is a vector-valued function on [0,1] that moves us
along the line segment. */
rr(q1, q2, t) := [
	q1[1] + t * (q2[1] - q1[1]),
	q1[2] + t * (q2[2] - q1[2])];

/** d(rr)/d(t): Used to line-integrate with parameterization */
drr_dt(q1, q2, t) := diff(rr(q1, q2, t), t);

/* See definition of line integral with parameterization:
    https://en.wikipedia.org/wiki/Line_integral */
integrate_side(q1, q2) := integrate(
	FF(rr(q1, q2, t)) . drr_dt(q1, q2, t),
	t, 0, 1);

/* This is the answer we want: line integral along just one side as a
function of the beginning point (q0x, q0y), end point (q1x, q1y) and
plan coefficients A, B and C. */
answer() := integrate_side([q0x,q0y], [q1x,q1y]);

/* Separate into coefficients for A, B and C. */
A_coeff = subst(1,A, subst(0,B, subst(0,C, answer())));
B_coeff = subst(0,A, subst(1,B, subst(0,C, answer())));
C_coeff = subst(0,A, subst(0,B, subst(1,C, answer())));
